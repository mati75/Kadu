; -*- coding: cp-1250 -*-

; Comment out when testing this script. It makes installer generation very slow.
SetCompressor /SOLID /FINAL lzma

!define PRODUCT_NAME "Kadu"
!define PRODUCT_VERSION "@KADU_VERSION@"
!define PRODUCT_BINARY_FILE_NAME "kadu.exe"
!define PRODUCT_UNINSTALLER_FILE_NAME "uninst.exe"
!define PRODUCT_PUBLISHER "Kadu Team"
!define PRODUCT_WEB_SITE "http://www.kadu.im"
!define PRODUCT_REGISTRY_ROOT "HKLM"
!define PRODUCT_REGISTRY_KEY_DIRECTORY "Software\Microsoft\Windows\CurrentVersion\App Paths\${PRODUCT_BINARY_FILE_NAME}"
!define PRODUCT_REGISTRY_KEY_UNINSTALL "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_REGISTRY_STARTMENU_VALUE "NSIS:StartMenuDir"
!define PRODUCT_INSTALLATION_OFFICIAL_SOURCE_DIR "@CMAKE_INSTALL_PREFIX@"
; !define PRODUCT_INSTALLATION_EXTERNAL_PLUGINS_SOURCE_DIR "C:\kadu\external-plugins\install"

!define BIN_DIR "${PRODUCT_INSTALLATION_OFFICIAL_SOURCE_DIR}\bin"
!define DATA_DIR "${PRODUCT_INSTALLATION_OFFICIAL_SOURCE_DIR}\share"
!define LIB_DIR "${PRODUCT_INSTALLATION_OFFICIAL_SOURCE_DIR}\lib"

; General Settings
Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "${PRODUCT_NAME}-${PRODUCT_VERSION}.exe"
InstallDir "$PROGRAMFILES\${PRODUCT_NAME}"
InstallDirRegKey ${PRODUCT_REGISTRY_ROOT} "${PRODUCT_REGISTRY_KEY_DIRECTORY}" ""
RequestExecutionLevel admin
XPStyle on
; Requires unreleased NSIS version
; TargetMinimalOS 5.0

; Modern UI
!include "MUI2.nsh"
!include "LogicLib.nsh"

; MUI Global Settings
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; MUI Language Settings
!define MUI_LANGDLL_REGISTRY_ROOT ${PRODUCT_REGISTRY_ROOT}
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_REGISTRY_KEY_DIRECTORY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "InstallerLanguage"

!insertmacro MUI_RESERVEFILE_LANGDLL

; MUI Installer Settings
!define MUI_ABORTWARNING
!define MUI_ABORTWARNING_CANCEL_DEFAULT

!insertmacro MUI_PAGE_WELCOME

!insertmacro MUI_PAGE_LICENSE "${DATA_DIR}\kadu\COPYING.WIN32"

!insertmacro MUI_PAGE_COMPONENTS

!insertmacro MUI_PAGE_DIRECTORY

Var START_MENU_FOLDER
!define MUI_STARTMENUPAGE_NODISABLE
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "${PRODUCT_NAME}"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_REGISTRY_ROOT}"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_REGISTRY_KEY_UNINSTALL}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "${PRODUCT_REGISTRY_STARTMENU_VALUE}"
!insertmacro MUI_PAGE_STARTMENU "Application" $START_MENU_FOLDER

!insertmacro MUI_PAGE_INSTFILES

!define MUI_FINISHPAGE_RUN "$INSTDIR\${PRODUCT_BINARY_FILE_NAME}"
!define MUI_FINISHPAGE_NOREBOOTSUPPORT
!insertmacro MUI_PAGE_FINISH

; MUI Uninstaller Settings
!insertmacro MUI_UNPAGE_CONFIRM

!insertmacro MUI_UNPAGE_INSTFILES

!insertmacro MUI_UNPAGE_FINISH

; MUI Languages
!insertmacro MUI_LANGUAGE "English" ;default
!insertmacro MUI_LANGUAGE "Polish"

; Language Strings
LangString ALREADY_INSTALLED_MESSAGE ${LANG_ENGLISH} "${PRODUCT_NAME} is already installed.$\n$\nClose ${PRODUCT_NAME} and click `OK` to remove the previous version and continue upgrade or `Cancel` to cancel this upgrade."
LangString ALREADY_INSTALLED_MESSAGE ${LANG_POLISH} "Program ${PRODUCT_NAME} jest ju¿ zainstalowany na tym systemie.$\n$\nZamknij program ${PRODUCT_NAME} i kliknij $\"OK$\", aby usun¹æ poprzedni¹ wersjê i kontynuowaæ aktualizacjê, lub $\"Anuluj$\", aby anulowaæ tê aktualizacjê."

LangString section_kadu ${LANG_ENGLISH} "Kadu"
LangString section_kadu ${LANG_POLISH} "Kadu"
LangString section_kadu_description ${LANG_ENGLISH} "Core Kadu components."
LangString section_kadu_description ${LANG_POLISH} "Podstawowe elementy Kadu."

LangString section_icon_themes ${LANG_ENGLISH} "Icon themes"
LangString section_icon_themes ${LANG_POLISH} "Zestawy ikon"
LangString section_icon_themes_description ${LANG_ENGLISH} "Various icon themes to use in Kadu"
LangString section_icon_themes_description ${LANG_POLISH} "Ró¿ne zestawy ikon do u¿ycia w Kadu"

LangString section_icon_themes_default ${LANG_ENGLISH} "Default"
LangString section_icon_themes_default ${LANG_POLISH} "Domyœlny"
LangString section_icon_themes_default_description ${LANG_ENGLISH} "Default Kadu icon theme"
LangString section_icon_themes_default_description ${LANG_POLISH} "Domyœlny zestaw ikon Kadu"

LangString section_sound_themes ${LANG_ENGLISH} "Sound themes"
LangString section_sound_themes ${LANG_POLISH} "Zestawy dŸwiêków"
LangString section_sound_themes_description ${LANG_ENGLISH} "Various sound themes to use in Kadu"
LangString section_sound_themes_description ${LANG_POLISH} "Ró¿ne zestawy dŸwiêków do u¿ycia w Kadu"

LangString section_sound_themes_default ${LANG_ENGLISH} "Default"
LangString section_sound_themes_default ${LANG_POLISH} "Domyœlny"
LangString section_sound_themes_default_description ${LANG_ENGLISH} "Default Kadu sound theme"
LangString section_sound_themes_default_description ${LANG_POLISH} "Domyœlny zestaw dŸwiêków Kadu"

LangString section_protocols ${LANG_ENGLISH} "Protocols"
LangString section_protocols ${LANG_POLISH} "Protoko³y"
LangString section_protocols_description ${LANG_ENGLISH} "Protocols supported by Kadu"
LangString section_protocols_description ${LANG_POLISH} "Protoko³y wspierane przez Kadu"

LangString section_plugins ${LANG_ENGLISH} "Plugins"
LangString section_plugins ${LANG_POLISH} "Wtyczki"
LangString section_plugins_description ${LANG_ENGLISH} "Official Kadu plugins"
LangString section_plugins_description ${LANG_POLISH} "Oficjalne wtyczki do Kadu"

LangString section_external_plugins ${LANG_ENGLISH} "External plugins"
LangString section_external_plugins ${LANG_POLISH} "Zewnêtrzne wtyczki"
LangString section_external_plugins_description ${LANG_ENGLISH} "External plugins for Kadu"
LangString section_external_plugins_description ${LANG_POLISH} "Zewnêtrzne wtyczki do Kadu"

LangString section_desktop_shortcut ${LANG_ENGLISH} "Desktop Shortcut"
LangString section_desktop_shortcut ${LANG_POLISH} "Skrót na pulpicie"

Function .onInit
	ReadRegStr $R0 ${PRODUCT_REGISTRY_ROOT} "${PRODUCT_REGISTRY_KEY_UNINSTALL}" "UninstallString"
	StrCmp $R0 "" done

	MessageBox MB_OKCANCEL|MB_ICONEXCLAMATION $(ALREADY_INSTALLED_MESSAGE) IDOK uninst
	Abort

;Run the uninstaller
uninst:
	!insertmacro MUI_LANGDLL_DISPLAY

	ClearErrors
	ExecWait '$R0 /S _?=$INSTDIR' ;Do not copy the uninstaller to a temp file

	IfErrors no_remove_uninstaller done
		;You can either use Delete /REBOOTOK in the uninstaller or add some code
		;here to remove the uninstaller. Use a registry key to check
		;whether the user has chosen to uninstall. If you are using an uninstaller
		;components page, make sure all sections are uninstalled.
	no_remove_uninstaller:

done:
FunctionEnd

Section -section_generic_libraries
	SetOverwrite on

	SetOutPath "$INSTDIR"
	File "${BIN_DIR}\libbz2-1.dll"
	File "${BIN_DIR}\libinjeqt.dll"
	File "${BIN_DIR}\libarchive-12.dll"
	File "${BIN_DIR}\libcrypto-10.dll"
	File "${BIN_DIR}\libffi-6.dll"
	File "${BIN_DIR}\libgcc_s_sjlj-1.dll"
	File "${BIN_DIR}\libgcrypt-20.dll"
	File "${BIN_DIR}\libglib-2.0-0.dll"
	File "${BIN_DIR}\libgmodule-2.0-0.dll"
	File "${BIN_DIR}\libgmp-10.dll"
	File "${BIN_DIR}\libgnutls-28.dll"
	File "${BIN_DIR}\libgnutls-openssl-27.dll"
	File "${BIN_DIR}\libgpg-error-0.dll"
	File "${BIN_DIR}\libhogweed-2-4.dll"
	File "${BIN_DIR}\libidn-11.dll"
	File "${BIN_DIR}\libintl-8.dll"
	File "${BIN_DIR}\libjpeg-8.dll"
	File "${BIN_DIR}\liblzma-5.dll"
	File "${BIN_DIR}\libnettle-4-6.dll"
	File "${BIN_DIR}\libp11-kit-0.dll"
	File "${BIN_DIR}\libpng16-16.dll"
	File "${BIN_DIR}\libprotobuf-c-1.dll"
	File "${BIN_DIR}\libqca-qt5.dll"
	File "${BIN_DIR}\libsqlite3-0.dll"
	File "${BIN_DIR}\libssl-10.dll"
	File "${BIN_DIR}\libstdc++-6.dll"
	File "${BIN_DIR}\libtasn1-6.dll"
	File "${BIN_DIR}\libtiff-5.dll"
	File "${BIN_DIR}\libwinpthread-1.dll"
	File "${BIN_DIR}\zlib1.dll"
	File "${BIN_DIR}\libotr-5.dll"
	File "${BIN_DIR}\libssp-0.dll"
	File "${BIN_DIR}\icudata53.dll"
	File "${BIN_DIR}\icui18n53.dll"
	File "${BIN_DIR}\icuio53.dll"
	File "${BIN_DIR}\icule53.dll"
	File "${BIN_DIR}\iculx53.dll"
	File "${BIN_DIR}\icutest53.dll"
	File "${BIN_DIR}\icutu53.dll"
	File "${BIN_DIR}\icuuc53.dll"
	File "${BIN_DIR}\libEGL.dll"
	File "${BIN_DIR}\libGLESv2.dll"
	File "${BIN_DIR}\libexslt-0.dll"
	File "${BIN_DIR}\libpcre-1.dll"
	File "${BIN_DIR}\libpcre16-0.dll"
	File "${BIN_DIR}\libwebp-4.dll"
	File "${BIN_DIR}\libxml2-2.dll"
	File "${BIN_DIR}\libxslt-1.dll"
SectionEnd

Section -section_qt
	SetOverwrite on

	SetOutPath "$INSTDIR"
	File "${BIN_DIR}\Qt5*.dll"
	File "${BIN_DIR}\qt.conf"

	SetOutPath "$INSTDIR\qt-plugins"
	File /r "${BIN_DIR}\qt-plugins\*"
SectionEnd

Section !$(section_kadu) SectionKadu
	SectionIn RO
	SetOverwrite on

; main files
	SetOutPath "$INSTDIR"
	File "${BIN_DIR}\kadu.exe"
	File "${BIN_DIR}\kadu_c.exe"
	File "${BIN_DIR}\libkadu.dll"
	File "${BIN_DIR}\kadu.ico"

; configuration
	SetOutPath "$INSTDIR\configuration"
	File "${DATA_DIR}\kadu\configuration\*.ui"

; scripts
	SetOutPath "$INSTDIR\scripts"
	File "${DATA_DIR}\kadu\scripts\*.js"

; syntax files
	SetOutPath "$INSTDIR\syntax"
	File /r "${DATA_DIR}\kadu\syntax\*"

; translations
	SetOutPath "$INSTDIR\translations"
	File "${DATA_DIR}\kadu\translations\*.qm"
	File "${DATA_DIR}\kadu\translations\*.language"

; qml files
	SetOutPath "$INSTDIR\qml"
	File /r "${DATA_DIR}\kadu\qml\*"

; other files
	SetOutPath "$INSTDIR"
	File "${DATA_DIR}\kadu\COPYING.WIN32"
	File "${DATA_DIR}\kadu\HISTORY"
	File "${DATA_DIR}\kadu\THANKS"
	File "${DATA_DIR}\kadu\AUTHORS"
	File "${DATA_DIR}\kadu\ChangeLog"
	File "${DATA_DIR}\kadu\AUTHORS.html"
	File "${DATA_DIR}\kadu\ChangeLog.OLD-PL"
	File "${DATA_DIR}\kadu\README"

; Shortcuts
	!insertmacro MUI_STARTMENU_WRITE_BEGIN "Application"
	CreateDirectory "$SMPROGRAMS\$START_MENU_FOLDER"
	CreateShortCut "$SMPROGRAMS\$START_MENU_FOLDER\${PRODUCT_NAME}.lnk" "$INSTDIR\${PRODUCT_BINARY_FILE_NAME}"
	!insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

!macro kadu_icon_theme_content name
	SetOverwrite on

	SetOutPath "$INSTDIR\themes\icons\${name}"
	File /r "${DATA_DIR}\kadu\themes\icons\${name}\*"
!macroend

!macro kadu_icon_theme name display_name
	Section ${name}
		!insertmacro kadu_icon_theme_content ${name}
	SectionEnd
!macroend

; dont forget to add new themes also to uninstall section
SectionGroup $(section_icon_themes) SectionIconThemes

	Section $(section_icon_themes_default) SectionIconThemesDefault
		SectionIn RO

		!insertmacro kadu_icon_theme_content default
	SectionEnd

	!insertmacro kadu_icon_theme faenza Faenza
	!insertmacro kadu_icon_theme glass Glass
	!insertmacro kadu_icon_theme oxygen Oxygen

SectionGroupEnd

; dont forget to add new themes also to uninstall section
SectionGroup $(section_sound_themes) SectionSoundThemes

	Section $(section_sound_themes_default) SectionSoundThemesDefault
		SectionIn RO
		SetOverwrite on

		SetOutPath "$INSTDIR\themes\sounds\default"
		File /r "${DATA_DIR}\kadu\themes\sounds\default\*"
	SectionEnd

SectionGroupEnd

!macro kadu_plugin_desc_file name
	SetOutPath "$INSTDIR\plugins\"
	File "${DATA_DIR}\kadu\plugins\${name}.desc"
!macroend

!macro kadu_plugin_ui_file name
	SetOutPath "$INSTDIR\plugins\configuration"
	File /nonfatal "${DATA_DIR}\kadu\plugins\configuration\${name}.ui"
!macroend

!macro kadu_plugin_translation_files name
	SetOutPath "$INSTDIR\plugins\translations"
	File /nonfatal "${DATA_DIR}\kadu\plugins\translations\${name}_cs.qm"
	File /nonfatal "${DATA_DIR}\kadu\plugins\translations\${name}_en.qm"
	File /nonfatal "${DATA_DIR}\kadu\plugins\translations\${name}_pl.qm"
	File /nonfatal "${DATA_DIR}\kadu\plugins\translations\${name}_tr.qm"
	File /nonfatal "${DATA_DIR}\kadu\plugins\translations\${name}_fr.qm"
!macroend

!macro kadu_plugin_data_files name
	SetOutPath "$INSTDIR\plugins\data\"
	File /nonfatal /r "${DATA_DIR}\kadu\plugins\data\${name}"
!macroend

!macro kadu_plugin_file name
	SetOutPath "$INSTDIR\plugins"
	File "${LIB_DIR}\kadu\plugins\${name}.dll"
!macroend

!macro kadu_plugin_content plugin_name
	SetOverwrite on

	!insertmacro kadu_plugin_desc_file ${plugin_name}
	!insertmacro kadu_plugin_ui_file ${plugin_name}
	!insertmacro kadu_plugin_translation_files ${plugin_name}
	!insertmacro kadu_plugin_data_files ${plugin_name}
	!insertmacro kadu_plugin_file lib${plugin_name}
!macroend

!macro kadu_plugin plugin_name plugin_display_name
	Section ${plugin_display_name}
		!insertmacro kadu_plugin_content ${plugin_name}
	SectionEnd
!macroend

!macro kadu_obligatory_plugin plugin_name plugin_display_name
	Section ${plugin_display_name}
		SectionIn RO
		!insertmacro kadu_plugin_content ${plugin_name}
	SectionEnd
!macroend

!macro kadu_external_plugin plugin_name plugin_display_name
	Section /o ${plugin_display_name}
		!insertmacro kadu_plugin_content ${plugin_name}
	SectionEnd
!macroend

SectionGroup $(section_protocols) SectionProtocols

	Section XMPP/Jabber
		!insertmacro kadu_plugin_content jabber_protocol

		WriteRegStr ${PRODUCT_REGISTRY_ROOT} "Software\Classes\xmpp\shell\open\command" "" "$INSTDIR\${PRODUCT_BINARY_FILE_NAME} $\"%1$\""
	SectionEnd

	Section Gadu-Gadu
		!insertmacro kadu_plugin_content gadu_protocol

		SetOutPath "$INSTDIR"
		File "${BIN_DIR}\libgadu-3.dll"

		WriteRegStr ${PRODUCT_REGISTRY_ROOT} "Software\Classes\gg\shell\open\command" "" "$INSTDIR\${PRODUCT_BINARY_FILE_NAME} $\"%1$\""
	SectionEnd

SectionGroupEnd

SectionGroup $(section_plugins) SectionPlugins

	!insertmacro kadu_plugin antistring antistring
	!insertmacro kadu_plugin auto_hide auto_hide
	!insertmacro kadu_plugin autoaway autoaway
	!insertmacro kadu_plugin autoresponder autoresponder
	!insertmacro kadu_plugin autostatus autostatus
	!insertmacro kadu_plugin cenzor cenzor
	!insertmacro kadu_plugin chat_notify chat_notify
	!insertmacro kadu_plugin config_wizard config_wizard
	!insertmacro kadu_plugin desktop_docking desktop_docking
	!insertmacro kadu_obligatory_plugin docking docking

	Section emoticons
		SetOverwrite on

		!insertmacro kadu_plugin_desc_file emoticons
		!insertmacro kadu_plugin_ui_file emoticons
		!insertmacro kadu_plugin_translation_files emoticons
		!insertmacro kadu_plugin_data_files emoticons
		!insertmacro kadu_plugin_file libemoticons

		; remember to add new themes also to uninstall section
		SetOutPath "$INSTDIR\themes\emoticons"
		File /r "${DATA_DIR}\kadu\themes\emoticons\*"
	SectionEnd

	!insertmacro kadu_obligatory_plugin encryption_ng encryption_ng

	Section encryption_ng_simlite
		SetOverwrite on

		!insertmacro kadu_plugin_desc_file encryption_ng_simlite
		!insertmacro kadu_plugin_ui_file encryption-ng-simlite
		!insertmacro kadu_plugin_translation_files encryption_ng_simlite
		!insertmacro kadu_plugin_data_files encryption_ng_simlite
		!insertmacro kadu_plugin_file libencryption_ng_simlite
	SectionEnd

	!insertmacro kadu_plugin encryption_otr encryption_otr
	!insertmacro kadu_plugin exec_notify exec_notify
	!insertmacro kadu_plugin ext_sound ext_sound
	!insertmacro kadu_plugin filedesc filedesc
	!insertmacro kadu_plugin firewall firewall

	Section hints
		SetOverwrite on

		!insertmacro kadu_plugin_desc_file hints
		!insertmacro kadu_plugin_ui_file hints
		!insertmacro kadu_plugin_ui_file hint-over-user
		!insertmacro kadu_plugin_ui_file hints-advanced
		!insertmacro kadu_plugin_ui_file hints-notifier
		!insertmacro kadu_plugin_translation_files hints
		!insertmacro kadu_plugin_data_files hints
		!insertmacro kadu_plugin_file libhints
	SectionEnd

	!insertmacro kadu_obligatory_plugin history history
	!insertmacro kadu_plugin history_migration history_migration
	!insertmacro kadu_obligatory_plugin idle idle

	Section imagelink
		SetOverwrite on

		!insertmacro kadu_plugin_desc_file imagelink
		!insertmacro kadu_plugin_ui_file image-link
		!insertmacro kadu_plugin_translation_files imagelink
		!insertmacro kadu_plugin_data_files imagelink
		!insertmacro kadu_plugin_file libimagelink
	SectionEnd

	!insertmacro kadu_plugin last_seen last_seen
	!insertmacro kadu_plugin mediaplayer mediaplayer
	!insertmacro kadu_plugin pcspeaker pcspeaker
	!insertmacro kadu_plugin profiles_import profiles_import
	!insertmacro kadu_plugin qt4_docking qt4_docking

	Section qt4_docking_notify
		SetOverwrite on

		!insertmacro kadu_plugin_desc_file qt4_docking_notify
		!insertmacro kadu_plugin_ui_file qt4-docking-notify
		!insertmacro kadu_plugin_translation_files qt4_docking_notify
		!insertmacro kadu_plugin_data_files qt4_docking_notify
		!insertmacro kadu_plugin_file libqt4_docking_notify
	SectionEnd

	!insertmacro kadu_plugin screenshot screenshot
	!insertmacro kadu_plugin simpleview simpleview
	!insertmacro kadu_plugin single_window single_window
	!insertmacro kadu_obligatory_plugin sms sms
	!insertmacro kadu_plugin sound sound
	!insertmacro kadu_plugin speech speech

	Section spellchecker
		SetOverwrite on

		!insertmacro kadu_plugin_desc_file spellchecker
		!insertmacro kadu_plugin_ui_file spellchecker
		!insertmacro kadu_plugin_translation_files spellchecker
		!insertmacro kadu_plugin_data_files spellchecker
		!insertmacro kadu_plugin_file libspellchecker

		SetOutPath "$INSTDIR"
		File "${BIN_DIR}\libenchant.dll"
		File "${BIN_DIR}\libhunspell-1.3-0.dll"

		SetOutPath "$INSTDIR\lib\enchant"
		File /r "${LIB_DIR}\enchant\*.dll"

		SetOutPath "$INSTDIR\share\enchant\myspell"
		File /r "${DATA_DIR}\enchant\myspell\*.aff"
		File /r "${DATA_DIR}\enchant\myspell\*.dic"
	SectionEnd

	!insertmacro kadu_obligatory_plugin sql_history sql_history
	!insertmacro kadu_plugin tabs tabs
	!insertmacro kadu_plugin winamp_mediaplayer winamp_mediaplayer
	!insertmacro kadu_plugin word_fix word_fix

SectionGroupEnd

SectionGroup $(section_external_plugins) SectionExternalPlugins

	!insertmacro kadu_external_plugin anonymous_check anonymous_check
	!insertmacro kadu_external_plugin import_history import_history
	!insertmacro kadu_external_plugin kadu_completion kadu_completion
#	!insertmacro kadu_external_plugin lednotify lednotify
#	!insertmacro kadu_external_plugin messagessplitter messagessplitter

	Section /o mime_tex
		!insertmacro kadu_plugin_content mime_tex

		SetOutPath "$INSTDIR\plugins\bin\mime_tex\"
		File /r "${LIB_DIR}\kadu\plugins\bin\mime_tex\mimetex.exe"
	SectionEnd

#	!insertmacro kadu_external_plugin networkping networkping
#	!insertmacro kadu_external_plugin nextinfo nextinfo
#	!insertmacro kadu_external_plugin senthistory senthistory

SectionGroupEnd

Section $(section_desktop_shortcut)
	SetOverwrite on
	CreateShortCut "$DESKTOP\${PRODUCT_NAME}.lnk" "$INSTDIR\${PRODUCT_BINARY_FILE_NAME}"
SectionEnd

Section -additional_links
	!insertmacro MUI_STARTMENU_WRITE_BEGIN "Application"
	WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
	CreateShortCut "$SMPROGRAMS\$START_MENU_FOLDER\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
	CreateShortCut "$SMPROGRAMS\$START_MENU_FOLDER\Uninstall.lnk" "$INSTDIR\${PRODUCT_UNINSTALLER_FILE_NAME}"
	!insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -Post
	WriteUninstaller "$INSTDIR\${PRODUCT_UNINSTALLER_FILE_NAME}"
	WriteRegStr ${PRODUCT_REGISTRY_ROOT} "${PRODUCT_REGISTRY_KEY_DIRECTORY}" "" "$INSTDIR\${PRODUCT_BINARY_FILE_NAME}"
	WriteRegStr ${PRODUCT_REGISTRY_ROOT} "${PRODUCT_REGISTRY_KEY_UNINSTALL}" "DisplayName" "$(^Name)"
	WriteRegStr ${PRODUCT_REGISTRY_ROOT} "${PRODUCT_REGISTRY_KEY_UNINSTALL}" "UninstallString" "$INSTDIR\${PRODUCT_UNINSTALLER_FILE_NAME}"
	WriteRegStr ${PRODUCT_REGISTRY_ROOT} "${PRODUCT_REGISTRY_KEY_UNINSTALL}" "DisplayIcon" "$INSTDIR\kadu.ico"
	WriteRegStr ${PRODUCT_REGISTRY_ROOT} "${PRODUCT_REGISTRY_KEY_UNINSTALL}" "DisplayVersion" "${PRODUCT_VERSION}"
	WriteRegStr ${PRODUCT_REGISTRY_ROOT} "${PRODUCT_REGISTRY_KEY_UNINSTALL}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
	WriteRegStr ${PRODUCT_REGISTRY_ROOT} "${PRODUCT_REGISTRY_KEY_UNINSTALL}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
	!insertmacro MUI_DESCRIPTION_TEXT ${SectionKadu} $(section_kadu_description)
	!insertmacro MUI_DESCRIPTION_TEXT ${SectionIconThemes} $(section_icon_themes_description)
	!insertmacro MUI_DESCRIPTION_TEXT ${SectionIconThemesDefault} $(section_icon_themes_default_description)
	!insertmacro MUI_DESCRIPTION_TEXT ${SectionSoundThemes} $(section_sound_themes_description)
	!insertmacro MUI_DESCRIPTION_TEXT ${SectionSoundThemesDefault} $(section_sound_themes_default_description)
!insertmacro MUI_FUNCTION_DESCRIPTION_END

Function un.onInit
	!insertmacro MUI_UNGETLANGUAGE
FunctionEnd

Section Uninstall
	!insertmacro MUI_STARTMENU_GETFOLDER "Application" $START_MENU_FOLDER
	Delete "$INSTDIR\*.*"

	Delete "$SMPROGRAMS\$START_MENU_FOLDER\Uninstall.lnk"
	Delete "$SMPROGRAMS\$START_MENU_FOLDER\Website.lnk"
	Delete "$DESKTOP\${PRODUCT_NAME}.lnk"
	Delete "$SMPROGRAMS\$START_MENU_FOLDER\${PRODUCT_NAME}.lnk"

	RMDir "$SMPROGRAMS\$START_MENU_FOLDER"
; some of these are from older versions
	RMDir /r "$INSTDIR\aspell"
	RMDir /r "$INSTDIR\configuration"
	RMDir /r "$INSTDIR\lib"
	RMDir /r "$INSTDIR\plugins"
	RMDir /r "$INSTDIR\qml"
	RMDir /r "$INSTDIR\qt-plugins"
	RMDir /r "$INSTDIR\scripts"
	RMDir /r "$INSTDIR\share"
	RMDir /r "$INSTDIR\syntax"

; remove themes one-by one
; just to keep user-added ones in place
; this does not makes us happy, but we don't want to remove user data
	RMDir /r "$INSTDIR\themes\emoticons\penguins"
	RMDir /r "$INSTDIR\themes\emoticons\tango"
	RMDir "$INSTDIR\themes\emoticons"

	RMDir /r "$INSTDIR\themes\icons\default"
	RMDir /r "$INSTDIR\themes\icons\faenza"
	RMDir /r "$INSTDIR\themes\icons\glass"
	RMDir /r "$INSTDIR\themes\icons\oxygen"
	RMDir "$INSTDIR\themes\icons"

	RMDir /r "$INSTDIR\themes\sounds\default"
	RMDir "$INSTDIR\themes\sounds"

	RMDir "$INSTDIR\themes"

	RMDir /r "$INSTDIR\translations"
	RMDir "$INSTDIR"

	DeleteRegKey ${PRODUCT_REGISTRY_ROOT} "${PRODUCT_REGISTRY_KEY_UNINSTALL}"
	DeleteRegKey ${PRODUCT_REGISTRY_ROOT} "${PRODUCT_REGISTRY_KEY_DIRECTORY}"

	ReadRegStr $R0 ${PRODUCT_REGISTRY_ROOT} "Classes\xmpp\shell\open\command" ""
	StrCmp $R0 "$INSTDIR\${PRODUCT_BINARY_FILE_NAME} $\"%1$\"" remove_xmpp
		Goto xmpp_removed
	remove_xmpp:
		DeleteRegKey ${PRODUCT_REGISTRY_ROOT} "Software\Classes\xmpp" ""
	xmpp_removed:

	ReadRegStr $R0 ${PRODUCT_REGISTRY_ROOT} "Software\Classes\gg\shell\open\command" ""
	StrCmp $R0 "$INSTDIR\${PRODUCT_BINARY_FILE_NAME} $\"%1$\"" remove_gg
		Goto gg_removed
	remove_gg:
		DeleteRegKey ${PRODUCT_REGISTRY_ROOT} "Software\Classes\gg" ""
	gg_removed:

	SetAutoClose true
SectionEnd
